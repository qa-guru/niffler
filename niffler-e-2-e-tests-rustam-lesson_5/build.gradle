buildscript {
    ext {
        protobufVersion = '3.21.12'
        grpcVersion = '1.52.1'
        allureVersion = '2.21.0'
        selenideVersion = '6.17.0'
        retrofitVersion = '2.9.0'
        springComponentsVersion = '6.0.3'
    }
}

plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id 'io.qameta.allure-adapter' version '2.11.2'
}

group 'guru.qa'
version '0.0.4-SNAPSHOT'

repositories {
    mavenCentral()
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

allure {
    version.set("${allureVersion}")
    adapter {
        aspectjVersion.set('1.9.19')
        autoconfigure.set(true)
        aspectjWeaver.set(true)
    }
}

configurations {
    jaxb
}

tasks.register('genJaxb') {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "userdata.wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "guru.qa.niffler.userdata.wsdl") {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 17, target: 17, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

sourceSets {
    test {
        java {
            srcDir 'src/test/java'
            srcDir 'build/generated-sources/jaxb'
        }
    }
}


dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.3'

    // REST
    testImplementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    testImplementation "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
    testImplementation 'com.squareup.okhttp3:logging-interceptor:3.14.9'
    testImplementation 'io.rest-assured:rest-assured:3.3.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    implementation 'com.github.javafaker:javafaker:1.0.2'
    // DB
    testImplementation "org.springframework.security:spring-security-crypto:${springComponentsVersion}"
    testImplementation "org.springframework:spring-jdbc:${springComponentsVersion}"
    testImplementation 'org.postgresql:postgresql:42.5.3'
    testImplementation 'p6spy:p6spy:3.9.1'
    testImplementation 'org.hibernate:hibernate-core:6.1.7.Final'
    testImplementation 'com.github.vertical-blank:sql-formatter:2.0.3'
    implementation 'commons-io:commons-io:2.11.0'
    // Allure
    testImplementation "io.qameta.allure:allure-attachments:${allureVersion}"
    testImplementation("io.qameta.allure:allure-junit5:${allureVersion}") {
        exclude group: "org.junit.jupiter-api"
        exclude group: "org.junit.platform"
        exclude group: "org.junit.jupiter"
    }
    testImplementation("io.qameta.allure:allure-selenide:${allureVersion}") {
        exclude group: 'com.codeborne'
    }
    testImplementation "io.qameta.allure:allure-okhttp3:${allureVersion}"
    testImplementation "io.qameta.allure:allure-selenide:${allureVersion}"
    testImplementation "io.qameta.allure:allure-grpc:${allureVersion}"
    // WEB
    testImplementation "com.codeborne:selenide:${selenideVersion}"
    // Utils
    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation 'com.google.code.findbugs:jsr305:3.0.2'
    // gRPC
    testImplementation "io.grpc:grpc-protobuf:${grpcVersion}"
    testImplementation "io.grpc:grpc-stub:${grpcVersion}"
    testImplementation "io.grpc:grpc-netty:${grpcVersion}"
    testImplementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    testImplementation project(':niffler-grpc-common')
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
    // SOAP
    testImplementation 'com.sun.xml.messaging.saaj:saaj-impl:3.0.1'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    // Wiremock
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.35.0'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'
    // SOAP
    testImplementation 'com.sun.xml.messaging.saaj:saaj-impl:3.0.1'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    implementation(files(genJaxb.classesDir).builtBy(genJaxb))
    jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.1'
    // Kafka
    testImplementation 'org.apache.kafka:kafka-clients:3.5.0'
}

test {
    options {
        systemProperties(System.getProperties())
    }
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform()
}
