plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.palantir.docker' version '0.34.0'
}

group 'guru.qa'
version '0.0.3-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "userdata.wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "niffler.userdata.wsdl") {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 17, target: 17, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated-sources/jaxb'
        }
    }
}

dependencies {
    implementation project(':niffler-grpc-common')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('org.springframework.boot:spring-boot-starter-web-services') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation "org.springframework.boot:spring-boot-starter-graphql"
    implementation 'com.tailrocks.graphql:graphql-datetime-spring-boot-starter:6.0.0'
    implementation 'org.springframework.ws:spring-ws-core'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation 'net.devh:grpc-client-spring-boot-starter:2.14.0.RELEASE'
    implementation "org.springframework:spring-webflux"
    implementation "io.projectreactor.netty:reactor-netty"
    // For Java 11:
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.1'
    implementation(files(genJaxb.classesDir).builtBy(genJaxb))

    jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

docker {
    dependsOn build
    name "dtuchs/niffler-gateway:${project.version}"
    tag 'latest', 'dtuchs/niffler-gateway:latest'
    files bootJar.archiveFile
    buildArgs([APP_VER: project.version, DOCKER: project.ext.dockerImage])
    noCache true
}

task dockerRmi(type: Exec) {
    workingDir "${buildDir}"
    commandLine 'bash', '-c', "docker rmi -f \$(docker images | grep 'niffler')\n"
}

tasks.named('bootJar') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('test') {
    useJUnitPlatform()
}
